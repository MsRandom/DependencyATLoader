buildscript {
    repositories {
        maven {
            url = "https://files.minecraftforge.net/maven"
        }
        mavenCentral()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:5.+"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "maven-publish"

def build = System.getenv("BUILD_NUMBER") ?: "0"

version = "1.0.0${build == "0" ? "" : "-" + build}"
group = "net.msrandom.atload"
archivesBaseName = "DependencyATLoader"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: "snapshot", version: "20180814-1.12"
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2855"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

if (hasProperty("maven_username") && hasProperty("maven_password")) {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = group
                artifactId = archivesBaseName

                from components.java
                artifact sourcesJar
            }
        }

        repositories {
            maven {
                url = "https://maven.msrandom.net/repository/root/"
                credentials {
                    username = maven_username
                    password = maven_password
                }
            }
        }
    }
}

if (build != "0") {
    tasks.build.finalizedBy publish
}
